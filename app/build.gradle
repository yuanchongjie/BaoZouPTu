apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    /*兼容Android6.0系统所需，如果这句话报错，可在dependencies标签下使用compile 'cn.bmob.android:http-legacy:1.0'*/
    useLibrary 'org.apache.http.legacy'
    defaultConfig {
        applicationId "a.baozouptu"
        minSdkVersion 16
        targetSdkVersion 19
        versionCode 1
        versionName "1.0"
        /**
         * 只支持中文，减小包大小
         */
        resConfigs "zh"
        ndk{
            abiFilters "armeabi"
        }
    }
    signingConfigs {
        release {
            storeFile file("androidApk-key1.jks")
            storePassword "111111"
            keyAlias "111111"
            keyPassword "111111"
        }
    }
    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled true //开启代码混淆
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.release
            jniDebuggable false
        }
    }
    productFlavors {
        tencent {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "tencent"]
        }
        _360 {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "_360"]
        }
        baidu {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "baidu"]
        }
    }
    //删除META-INF信息
    packagingOptions {
        exclude 'META-INF/rxjava.properties'
        exclude 'META-INF/maven/com.squareup.okhttp3/okhttp/pom.xml'
        exclude 'META-INF/maven/com.squareup.okhttp3/okhttp/pom.properties'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.xml'
        exclude 'META-INF/maven/com.squareup.okio/okio/pom.properties'
        exclude 'META-INF/maven/com.google.code.gson/gson/pom.xml'
        exclude 'META-INF/maven/com.google.code.gson/gson/pom.properties'
    }
    sourceSets { main { jni.srcDirs = ['src/main/jni', 'src/main/jniLibs/'] } }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    //bmob-sdk：Bmob的android sdk包，包含了Bmob的数据存储、文件等服务，以下是最新的bmob-sdk:
    //3.5.0：请务必查看下面注释[1]
    compile 'cn.bmob.android:bmob-sdk:3.5.0'
    //如果你想应用能够兼容Android6.0，请添加此依赖(org.apache.http.legacy.jar)
    compile 'cn.bmob.android:http-legacy:1.0'

    //官方的
    compile 'com.android.support:appcompat-v7:25.1.0'
    compile 'com.android.support:design:25.1.0'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.android.support.constraint:constraint-layout:1.0.0-beta4'

    //腾讯qq开放平台的sdk
    compile files('src/main/libs/open_sdk_r5756.jar')
    compile project(':openCVLibrary310')
    compile fileTree(dir: "$buildDir/native-libs", include: 'native-libs.jar')

    testCompile 'junit:junit:4.12'
}

task nativeLibsToJar(type: Jar, description: 'create a jar archive of the native libs') {
    destinationDir file("$buildDir/native-libs")
    baseName 'native-libs'
    from fileTree(dir: 'libs', include: 'armeabi/*.so')
    into 'lib/'
}


tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn(nativeLibsToJar)
}